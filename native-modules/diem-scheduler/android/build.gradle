import groovy.json.JsonSlurper

buildscript {
  repositories {
    google()
    mavenCentral()
  }
  dependencies {
    classpath "com.android.tools.build:gradle:9.0.0"
  }
}

ext {
  minSdkVersion = 26
  targetSdkVersion = 36
  compileSdkVersion = 36
}

apply plugin: "com.android.library"

def reactNativeVersion = new JsonSlurper()
  .parse(file("../../../node_modules/react-native/package.json"))
  .version

android {
  namespace "com.diem.scheduler"
  compileSdkVersion 36
  
  defaultConfig {
    minSdkVersion 26
    targetSdkVersion 36

    externalNativeBuild {
      cmake {
        arguments "-DANDROID_STL=c++_shared", "-DANDROID_SUPPORT_FLEXIBLE_PAGE_SIZES=ON"
        cppFlags "-std=c++20"
        abiFilters "arm64-v8a", "x86_64"
      }
    }
  }

  buildFeatures {
    prefab true
  }

  externalNativeBuild {
    cmake {
      path "src/main/cpp/CMakeLists.txt"
    }
  }
  
  packagingOptions {
    excludes = ["**/libc++_shared.so", "**/libreactnative.so", "**/libjsi.so"]
  }
}

repositories {
  google()
  mavenCentral()
}

allprojects {
  configurations.configureEach {
    resolutionStrategy.dependencySubstitution {
      substitute(module("com.facebook.react:react-native"))
        .using(module("com.facebook.react:react-android:${reactNativeVersion}"))
    }
  }
}

dependencies {
  implementation "com.facebook.react:react-android:${reactNativeVersion}"
  implementation project(":react-native-nitro-modules")
}

apply from: "../../../nitrogen/generated/android/diem_scheduler+autolinking.gradle"
